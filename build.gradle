plugins {
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'jacoco'
    id 'java'
    id 'idea'
    id 'org.sonarqube' version '3.3'
    id 'org.springframework.boot' version '1.5.22.RELEASE'
}

clean {
    delete 'data'
}

license {
    ignoreFailures true
    header = file('LICENSE')
}

jar {
    archiveBaseName = 'pivio-server'
    archiveVersion = '1.1.0'
}
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

//TODO: Remove override of versions provided by Spring Boot dependency management with Spring Boot update.
ext['assertj.version'] = '3.9.0'

dependencies {
    compile 'com.flipkart.zjsonpatch:zjsonpatch:0.4.11'
    compile 'org.apache.commons:commons-lang3:3.12.0'
    compile 'org.elasticsearch.plugin:delete-by-query:' + project.properties["dep.elasticsearch"]
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    compile 'org.springframework.boot:spring-boot-starter-web'

    testCompile 'org.apache.commons:commons-text:1.9'
    testCompile 'org.awaitility:awaitility:4.1.1'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.testcontainers:testcontainers:1.16.2'
}

// see https://github.com/ben-manes/gradle-versions-plugin
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

tasks.withType(Test) {
    systemProperty 'gradleIsRunning', true
}

tasks.named('wrapper') {
    gradleVersion = '6.9'
    distributionType = Wrapper.DistributionType.ALL
}
